AC_INIT(libgnomeui)

# Making releases:
#   LIBGNOMEUI_MICRO_VERSION += 1;
#   LIBGNOMEUI_INTERFACE_AGE += 1;
#   LIBGNOMEUI_BINARY_AGE += 1;
# if any functions have been added, set LIBGNOMEUI_INTERFACE_AGE to 0.
# if backwards compatibility has been broken,
# set LIBGNOMEUI_BINARY_AGE and LIBGNOMEUI_INTERFACE_AGE to 0.
#
LIBGNOMEUI_MAJOR_VERSION=2
LIBGNOMEUI_MINOR_VERSION=4
LIBGNOMEUI_MICRO_VERSION=0
LIBGNOMEUI_INTERFACE_AGE=0
# If you need a modifier for the version number. 
# Normally empty, but can be used to make "fixup" releases.
LIBGNOMEUI_EXTRAVERSION=

dnl libtool versioning from libgnome

LIBGNOMEUI_CURRENT=`expr 100 '*' $LIBGNOMEUI_MINOR_VERSION + $LIBGNOMEUI_MICRO_VERSION - $LIBGNOMEUI_INTERFACE_AGE`
LIBGNOMEUI_BINARY_AGE=`expr 100 '*' $LIBGNOMEUI_MINOR_VERSION + $LIBGNOMEUI_MICRO_VERSION`
LIBGNOMEUI_REVISION=$LIBGNOMEUI_INTERFACE_AGE
LIBGNOMEUI_AGE=`expr $LIBGNOMEUI_BINARY_AGE - $LIBGNOMEUI_INTERFACE_AGE`
LIBGNOMEUI_VERSION=$LIBGNOMEUI_MAJOR_VERSION.$LIBGNOMEUI_MINOR_VERSION.$LIBGNOMEUI_MICRO_VERSION$LIBGNOMEUI_EXTRAVERSION

AC_SUBST(LIBGNOMEUI_CURRENT)
AC_SUBST(LIBGNOMEUI_REVISION)
AC_SUBST(LIBGNOMEUI_AGE)

AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(libgnomeui, $LIBGNOMEUI_VERSION)

AM_MAINTAINER_MODE

AC_ISC_POSIX
AC_PROG_CC
AC_STDC_HEADERS
AC_LIBTOOL_WIN32_DLL
AM_PROG_LIBTOOL
AC_PROG_YACC
AC_PATH_PROGS(PATH_TO_XRDB, "xrdb")

GETTEXT_PACKAGE=libgnomeui-2.0
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE")

ALL_LINGUAS="am ar az be bg bn ca cs cy da de el en_GB eo es et fa fi fr ga he hi hu id is it ja kn ko li lt lv mk ml mn ms nl nn no pl pt pt_BR ro ru sk sl sq sr sr@Latn sv ta tr uk vi wa yi zh_CN zh_TW"
AM_GLIB_GNU_GETTEXT
gnomelocaledir='${prefix}/${DATADIRNAME}/locale'
AC_SUBST(gnomelocaledir)

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

GNOME_COMPILE_WARNINGS

dnl Define GNOME_ENABLE_DEBUG if the --enable-debug switch was given.
GNOME_DEBUG_CHECK

dnl
dnl Check for -lSM (for gnome-client)
dnl

AC_PATH_XTRA

SM_LIBS=""
sm_save_cppflags="$CPPFLAGS"
CPPFLAGS="$CPPFLAGS $X_CFLAGS"

GNOME_HAVE_SM=true
AC_CHECK_LIB(SM, SmcSaveYourselfDone,
	[SM_LIBS="$X_LIBS -lSM -lICE"],GNOME_HAVE_SM=false,
	$X_LIBS -lICE)
AC_CHECK_HEADERS(X11/SM/SMlib.h,,GNOME_HAVE_SM=false)
if test "$GNOME_HAVE_SM" = true; then
	AC_DEFINE(HAVE_LIBSM)
fi

AC_SUBST(SM_LIBS)
CPPFLAGS="$sm_save_cppflags"

dnl
dnl Start of pkg-config checks
dnl

dnl We first check for esound and audiofile (which has conditional
dnl support built in)
dnl
AUDIOFILE_MODULE="audiofile >= 0.2.3"
ESOUND_MODULE="esound >= 0.2.26"
PKG_CHECK_MODULES(SOUND_TEST, $ESOUND_MODULE $AUDIOFILE_MODULE, [
	AC_DEFINE(HAVE_LIBAUDIOFILE)
	AC_DEFINE(HAVE_ESD)
	esd_msg=yes
],[
	AUDIOFILE_MODULE=""
	ESOUND_MODULE=""
	esd_msg=no
])

dnl
dnl libjpeg
dnl
  if test -z "$LIBJPEG"; then
    AC_CHECK_LIB(jpeg, jpeg_start_decompress,
      [AC_CHECK_HEADER(jpeglib.h,
        jpeg_ok=yes,
        jpeg_ok=no)],
      AC_MSG_WARN(*** (jpeg library not found) ***), -lm)
    if test "$jpeg_ok" = yes; then
      JPEG='jpeg'; LIBJPEG='-ljpeg'
      AC_DEFINE(HAVE_LIBJPEG)
    else
     AC_MSG_WARN(*** JPEG thumbnail loader will not be built (jpeg header file not found) ***)
    fi
  fi

AC_SUBST(LIBJPEG)

GNOMEUI_MODULES="libgnome-2.0 >= 2.0.0 libgnomecanvas-2.0 >= 2.0.0 libbonoboui-2.0 >= 2.0.0 gconf-2.0 >= 1.1.11 pango >= 1.1.2"
PKG_CHECK_MODULES(LIBGNOMEUI, $GNOMEUI_MODULES)
LIBGNOMEUI_LIBS="$LIBGNOMEUI_LIBS $LIBJPEG"
AC_SUBST(LIBGNOMEUI_CFLAGS)
AC_SUBST(LIBGNOMEUI_LIBS)

PKG_CHECK_MODULES(LIBGNOMEUI_COMPONENTS, $GNOMEUI_MODULES)
AC_SUBST(LIBGNOMEUI_COMPONENTS_CFLAGS)
AC_SUBST(LIBGNOMEUI_COMPONENTS_LIBS)

PKG_CHECK_MODULES(LIBGNOMEUI_DEMOS, $GNOMEUI_MODULES)
AC_SUBST(LIBGNOMEUI_DEMOS_CFLAGS)
AC_SUBST(LIBGNOMEUI_DEMOS_LIBS)

PKG_CHECK_MODULES(LIBGNOMEUI_GLADE, $GNOMEUI_MODULES libglade-2.0 >= 2.0.0)
AC_SUBST(LIBGNOMEUI_GLADE_CFLAGS)
AC_SUBST(LIBGNOMEUI_GLADE_LIBS)
AC_PATH_PROG(LIBGLADE_CONVERT, libglade-convert)
if test -z "$LIBGLADE_CONVERT" ; then
	AC_MSG_WARN([libglade-convert (from libglade 2.0) is needed for the checks in the tests directory.])
fi
	
dnl
dnl Check for gtk-doc
dnl
AC_ARG_WITH(html-dir, [  --with-html-dir=PATH path to installed docs ])

if test "x$with_html_dir" = "x" ; then
  HTML_DIR='${datadir}/gtk-doc/html'
else
  HTML_DIR=$with_html_dir
fi

AC_SUBST(HTML_DIR)

AC_CHECK_PROG(GTKDOC, gtkdoc-mkdb, true, false)

gtk_doc_min_version=0.6
if $GTKDOC ; then 
    gtk_doc_version=`gtkdoc-mkdb --version`
    AC_MSG_CHECKING([gtk-doc version ($gtk_doc_version) >= $gtk_doc_min_version])

    IFS="${IFS= 	}"; gtk_save_IFS="$IFS"; IFS="."
    set $gtk_doc_version
    for min in $gtk_doc_min_version ; do
        cur=$1; shift
        if test -z $min ; then break; fi
        if test -z $cur ; then GTKDOC=false; break; fi
        if test $cur -gt $min ; then break ; fi
        if test $cur -lt $min ; then GTKDOC=false; break ; fi
    done
    IFS="$gtk_save_IFS"

   if $GTKDOC ; then
      AC_MSG_RESULT(yes)
   else
      AC_MSG_RESULT(no)
   fi
fi

dnl Let people disable the gtk-doc stuff.
AC_ARG_ENABLE(gtk-doc, [  --enable-gtk-doc  Use gtk-doc to build documentation [default=auto]], enable_gtk_doc="$enableval", enable_gtk_doc=auto)

if test x$enable_gtk_doc = xauto ; then
  if test x$GTKDOC = xtrue ; then
    enable_gtk_doc=yes
  else
    enable_gtk_doc=no 
  fi
fi

AM_CONDITIONAL(ENABLE_GTK_DOC, test x$enable_gtk_doc = xyes)

dnl Specify the gconf configuration source, 
dnl default to xml::$(sysconfdir)/gconf/gconf.xml.defaults

GCONF_CONFIG_SOURCE=
AC_ARG_ENABLE(gconf-source, [  --enable-gconf-source=sourceaddress      Where to install schema files.],GCONF_CONFIG_SOURCE=$enable_gconf_source,)

if test "x$GCONF_CONFIG_SOURCE" = "x"; then
        GCONF_CONFIG_SOURCE="xml::\${DESTDIR}\${sysconfdir}/gconf/gconf.xml.defaults"
fi

AC_SUBST(GCONF_CONFIG_SOURCE)
AC_SUBST(INSTALL_GCONF_CONFIG_SOURCE)

dnl AC_SUBST GTK+ version
GTK_VERSION=`$PKG_CONFIG --modversion gtk+-2.0`
AC_SUBST(GTK_VERSION)

AC_MSG_CHECKING([for gtk+ multihead support])
if $PKG_CONFIG --atleast-version 2.1.0 gtk+-2.0; then
   AC_DEFINE(HAVE_GTK_MULTIHEAD,,[gtk+ with multihead support found])
   AC_MSG_RESULT(yes)
else
   AC_MSG_RESULT(no)
fi

AC_CHECK_LIB(popt, poptStrippedArgv,, AC_MSG_ERROR([popt 1.5 or newer is required to build
libgnomeui. You can download the latest version from ftp://ftp.rpm.org/pub/rpm/dist/rpm-4.0.x/]))

AC_CHECK_HEADERS(dlfcn.h dl.h utmp.h locale.h mcheck.h unistd.h)
AC_CHECK_FUNCS(bind_textdomain_codeset)

dnl This is installed from GTK+ 2.0's gdk-pixbuf
AC_PATH_PROG(GDK_PIXBUF_CSOURCE, gdk-pixbuf-csource)
test -z "$GDK_PIXBUF_CSOURCE" && AC_MSG_ERROR([
*** You need the gdk-pixbuf-csource tool which is installed
*** from GTK+ 2.0's gdk-pixbuf.
***
*** Either the location where you installed your GTK+ 2.0 is
*** not in your PATH or something is screwed up with your
*** GTK+ 2.0 installation
])

dnl Don't use AC_PROG_AWK since we need the full pathname.
AC_PATH_PROGS(AWK, mawk gawk nawk awk, )
AC_PATH_PROGS(PERL, perl5 perl)

AC_PATH_PROG(GCONFTOOL, gconftool-2)

# define a MAINT-like variable REBUILD which is set if Perl
# and awk are found, so autogenerated sources can be rebuilt
AC_ARG_ENABLE(rebuilds, [  --disable-rebuilds      disable all source autogeneration rules],,enable_rebuilds=yes)
REBUILD=\#
if test "x$enable_rebuilds" = "xyes" && \
     test -n "$PERL" && \
     $PERL -e 'exit !($] >= 5.002)' > /dev/null 2>&1 && \
     test -n "$AWK" ; then
  REBUILD=
fi
AC_SUBST(REBUILD)

AC_OUTPUT([
Makefile
po/Makefile.in
libgnomeui/Makefile
libgnomeui/libgnomeui-2.0.pc
libgnomeui/pixmaps/Makefile
glade/Makefile
images/Makefile
demos/Makefile
test-gnome/Makefile
doc/Makefile
doc/reference/Makefile
])
echo "configuration:
	esd support: ${esd_msg}
	doc build:   ${enable_gtk_doc}
"
