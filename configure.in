AC_INIT(libgnomeui)

AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(libgnomeui2, 1.103.0)

AM_MAINTAINER_MODE

GNOME_COMMON_INIT
GNOME_PLATFORM_GNOME_2(yes, force)

AC_ISC_POSIX
AC_PROG_CC
AC_STDC_HEADERS
AM_PROG_LIBTOOL
AC_PROG_YACC
AC_PATH_PROGS(PATH_TO_XRDB, "xrdb")

ALL_LINGUAS="az es fr it nn no sv ta tr wa zh_CN zh_TW"
AM_GLIB_GNU_GETTEXT

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

GNOME_COMPILE_WARNINGS(maximum)

dnl
dnl Check for -lSM (for gnome-client)
dnl

AC_PATH_XTRA

SM_LIBS=""
sm_save_cppflags="$CPPFLAGS"
CPPFLAGS="$CPPFLAGS $X_CFLAGS"

GNOME_HAVE_SM=true
AC_CHECK_LIB(SM, SmcSaveYourselfDone,
	[SM_LIBS="-lSM -lICE"],GNOME_HAVE_SM=false,
	$X_LIBS -lICE)
AC_CHECK_HEADERS(X11/SM/SMlib.h,,GNOME_HAVE_SM=false)
if test "$GNOME_HAVE_SM" = true; then
	AC_DEFINE(HAVE_LIBSM)
fi

AC_SUBST(SM_LIBS)
CPPFLAGS="$sm_save_cppflags"

dnl
dnl Start of pkg-config checks
dnl
PKG_CHECK_MODULES(LIBGNOMEUI, \
		  libgnome-2.0 >= 1.103.0 libgnomecanvas-2.0 >= 1.103.0 \
		  libbonoboui-2.0 >= 1.97.0 gconf-2.0 >= 1.1.1 )
AC_SUBST(LIBGNOMEUI_CFLAGS)
AC_SUBST(LIBGNOMEUI_LIBS)

PKG_CHECK_MODULES(LIBGNOMEUI_COMPONENTS, \
		  libgnome-2.0 >= 1.102.0 libgnomecanvas-2.0 >= 1.102.0 \
		  libbonoboui-2.0 >= 1.97.0 gconf-2.0 >= 1.1.1)
AC_SUBST(LIBGNOMEUI_COMPONENTS_CFLAGS)
AC_SUBST(LIBGNOMEUI_COMPONENTS_LIBS)

PKG_CHECK_MODULES(LIBGNOMEUI_DEMOS, \
		  libgnome-2.0 >= 1.102.0 libgnomecanvas-2.0 >= 1.102.0 \
		  libbonoboui-2.0 >= 1.97.0 gconf-2.0 >= 1.1.1)
AC_SUBST(LIBGNOMEUI_DEMOS_CFLAGS)
AC_SUBST(LIBGNOMEUI_DEMOS_LIBS)

dnl
dnl Check for gtk-doc
dnl
AC_ARG_WITH(html-dir, [  --with-html-dir=PATH path to installed docs ])

if test "x$with_html_dir" = "x" ; then
  HTML_DIR='${datadir}/gtk-doc/html'
else
  HTML_DIR=$with_html_dir
fi

AC_SUBST(HTML_DIR)

AC_CHECK_PROG(GTKDOC, gtkdoc-mkdb, true, false)

gtk_doc_min_version=0.6
if $GTKDOC ; then 
    gtk_doc_version=`gtkdoc-mkdb --version`
    AC_MSG_CHECKING([gtk-doc version ($gtk_doc_version) >= $gtk_doc_min_version])
    if perl <<EOF ; then
      exit (("$gtk_doc_version" =~ /^[[0-9]]+\.[[0-9]]+$/) &&
            ("$gtk_doc_version" >= "$gtk_doc_min_version") ? 0 : 1);
EOF
      AC_MSG_RESULT(yes)
   else
      AC_MSG_RESULT(no)
      GTKDOC=false
   fi
fi

dnl Let people disable the gtk-doc stuff.
AC_ARG_ENABLE(gtk-doc, [  --enable-gtk-doc  Use gtk-doc to build documentation [default=auto]], enable_gtk_doc="$enableval", enable_gtk_doc=auto)

if test x$enable_gtk_doc = xauto ; then
  if test x$GTKDOC = xtrue ; then
    enable_gtk_doc=yes
  else
    enable_gtk_doc=no 
  fi
fi

AM_CONDITIONAL(ENABLE_GTK_DOC, test x$enable_gtk_doc = xyes)


dnl AC_SUBST GTK+ version
GTK_VERSION=`$PKG_CONFIG --modversion gtk+-2.0`
AC_SUBST(GTK_VERSION)

AC_CHECK_LIB(popt, poptStrippedArgv,, AC_MSG_ERROR([popt 1.5 or newer is required to build
gnome-libs. You can download the latest version from ftp://people.redhat.com/sopwith/popt/]))

AC_CHECK_HEADERS(dlfcn.h dl.h utmp.h locale.h mcheck.h unistd.h)

dnl This is installed from GTK+ 2.0's gdk-pixbuf
AC_PATH_PROG(GDK_PIXBUF_CSOURCE, gdk-pixbuf-csource)
test -z "$GDK_PIXBUF_CSOURCE" && AC_MSG_ERROR([
*** You need the gdk-pixbuf-csource tool which is installed
*** from GTK+ 2.0's gdk-pixbuf.
***
*** Either the location where you installed your GTK+ 2.0 is
*** not in your PATH or something is screwed up with your
*** GTK+ 2.0 installation
])

dnl Don't use AC_PROG_AWK since we need the full pathname.
AC_PATH_PROGS(AWK, mawk gawk nawk awk, )
AC_PATH_PROGS(PERL, perl5 perl)

# define a MAINT-like variable REBUILD which is set if Perl
# and awk are found, so autogenerated sources can be rebuilt
AC_ARG_ENABLE(rebuilds, [  --disable-rebuilds      disable all source autogeneration rules],,enable_rebuilds=yes)
REBUILD=\#
if test "x$enable_rebuilds" = "xyes" && \
     test -n "$PERL" && \
     $PERL -e 'exit !($] >= 5.002)' > /dev/null 2>&1 && \
     test -n "$AWK" ; then
  REBUILD=
fi
AC_SUBST(REBUILD)

AC_OUTPUT([
Makefile
po/Makefile.in
libgnomeui/Makefile
libgnomeui/libgnomeui-2.0.pc
libgnomeui/pixmaps/Makefile
images/Makefile
demos/Makefile
test-gnome/Makefile
doc/Makefile
doc/reference/Makefile
])

cat $srcdir/message-of-doom
